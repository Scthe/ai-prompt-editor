// https://github.com/AUTOMATIC1111/stable-diffusion-webui/blob/cf2772fab0af5573da775e7437e6acdca424f26e/modules/prompt_parser.py#L15
// Cheatsheet: https://lark-parser.readthedocs.io/en/latest/_static/lark_cheatsheet.pdf
// Note: rules are only lowercase, add them to 'prompt' alternative

// LALR: removed '*' at the end, I think one in 'prompt' should be enough?
!start: (prompt | /[][():]/+)
// LALR: Added 'scheduledremove', see below
prompt: (emphasized | scheduled | alternate | scheduledremove | plain | WHITESPACE)*
!emphasized: "(" prompt ")"
        | "(" prompt ":" prompt ")"
        | "[" prompt "]"
scheduled: "[" [prompt ":"] prompt ":" [WHITESPACE] NUMBER [WHITESPACE] "]"
// LALR: New rule, LALR does not allow `[from::0.25]` with `scheduled`
scheduledremove: "[" prompt "::" [WHITESPACE] NUMBER [WHITESPACE] "]"
// LALR: Changed to limit recursions. Are you really have more than 6 alternates?
alternate: "[" prompt ("|" [prompt])~1..5 "]"
WHITESPACE: /\s+/
plain: /([^\\\[\]():|]|\\.)+/
%import common.SIGNED_NUMBER -> NUMBER
